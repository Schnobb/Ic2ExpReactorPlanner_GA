apply plugin: 'java'
apply plugin: 'application'

application {
    mainClass = 'Ic2ExpReactorPlanner.ReactorPlannerFrame'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.release = 25
}

sourceSets {
    main {
        java { srcDirs 'src' }
        resources { srcDir 'src' }
    }
    test {
        java { srcDir 'test' }
        resources { srcDir 'testResources' }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.9'
    testImplementation group: 'junit', name: 'junit', version: '4.10'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
}

test {
    useJUnit()
}

jar {
    manifest {
        attributes 'Main-Class': 'Ic2ExpReactorPlanner.ReactorPlannerFrame'
    }
}

task processSource(type: Copy) {
    from sourceSets.main.java
    def version = project.hasProperty('erpVersion') ? erpVersion : 'dev'
    inputs.property 'erpVersion', version
    filter { line -> line.replaceAll('@VERSION@', version) }
    into "$buildDir/generated-src"
}

compileJava {
    dependsOn processSource
    source = processSource.outputs.files
}